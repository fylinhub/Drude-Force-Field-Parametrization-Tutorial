* Water interactions
* 

ioformat extended

system "mkdir wat"
set residue clet
set bombover 0 !needs to be set to -1 for 3-membered rings

set stage 5 
            

set NoDrude false

!### Things that usually shouldn't be changed. ####
set dontmini 0 ! Don't minimize main conformation. Should normally not be used.

!non-bond parameters
set 3 999.0  ! cutim
set 4 999.0  ! cutnb
set 5 980.0  ! ctonnb
set 6 990.0  ! ctofnb
set 7 switch
set 8 atom
set 9 vatom

!stream in files
    
    stream toppar_drude_master_example.str 
    stream toppar_drude_example.str


!SINCE inter is not correctly implemented for drude ff, the interaction should be computed:
!interaction = E_mol_wat - E_wat - E_mol
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!compute the energy of water firstly: in fact, the energy for swm4 is 0.00
read sequence card
* water
*
1
SWM4

generate SWM4 first none last none setup warn noangle nodihedral drude dmass 0.4

ic seed swm4 1 h1 swm4 1 oh2 swm4 1 h2
ic build
if NoDrude .eq. false then
   coor sdrude
   coor shake
endif
coor print

update cdie inbfrq -1 ihbfrq 0 -
@7 @8 @9 vswitch cutnb @4 ctofnb @6 ctonnb @5

cons harm force 1000.0 sele .not. type D* show end
cons harm force 1000.0 sele type dum show end
MINI SD nstep 200 tolgrd 0.00001
MINI ABNR nstep 500 tolgrd 0.00001
cons harm force 0.0 sele all end
coor print

energy

set wele ?elec
set wvdw ?vdw
set wener ?ener
write coor pdb name swm4.pdb

delete atom select all end
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!read sequence @residue 1
read sequence card
* @residue
*
1
@residue

bomlev @bombover
generate  @residue first none last none setup warn drude dmass 0.4
bomlev 0

!read coor card name @residue_mp2.crd
open read card unit 21 name @residue_mp2.crd
read coor card unit 21

if NoDrude .eq. false then
   coor sdrude
   coor shake
endif
coor copy comp
coor print ! use to make sure parent coords are not modified

update cdie inbfrq -1 ihbfrq 0 -
@7 @8 @9 vswitch cutnb @4 ctofnb @6 ctonnb @5

if @stage eq 4 then
  open unit 50 write form name @residue_water_constr.ene
else
  if @dontmini eq 0 then
    ! minimize Drude coordinates
    cons harm force 10000000.0 sele .not. type D* show end
    cons harm force 10000000.0 sele type dum show end
    MINI SD nstep 200 tolgrd 0.00001
    MINI ABNR nstep 500 tolgrd 0.00001
    cons harm force 0.0 sele all end
  endif
  if @stage ne 5 stop
  open unit 50 write form name @residue_water.ene
endif

!compute the energy for the molecule only
energy

set mele ?elec
set mvdw ?vdw
set mener ?ener

!water interactions: variables for averages etc.
set count 0
set asum 0.0
set sum 0.0
set sum2 0.0

coor dipole oxyz select segid @residue end
echu 50
echo @residue water interaction
if @stage eq 4 echo ######## USING MP2 GEOMETRY!!! ########
echo QM dipole (Debye)
echo mp2/cc-pvQZ dipole: X= -1.1386 Y=1.7948 Z=0.0000  Tot=2.1254 
echo empirical dipole: X=?xdip Y=?ydip Z=?zdip Tot=?rdip
echo

stream polar.efield.str

echo QM alph(scaled)
echo mp2/cc-pvQZ alpha: XX= 6.249 YY= 6.359 ZZ= 4.962 Tot= 5.857
echo empirical  alpha: xx= @alphaxx yy= @alphayy  zz=  @alphazz  tot= @malpha
echo

calc xperct = 6.249/@alphaxx
calc yperct = 6.359/@alphayy
calc zperct = 4.962/@alphazz
echo qm1/mm-alpha     XX= @xperct YY= @yperct ZZ= @zperct 
echo 


ic delete sele all end
coor copy comp
!!! ***** BEGIN of water interactions ***** !!!

!1) H13...OH2 

set d 1.60 ! initial interaction distance
coor copy

read sequence card
* water
*
1
SWM4

generate SWM4 first none last none setup warn noangle nodihedral drude dmass 0.4

join @residue SWM4 renumber

read sequence card
* dummy
*
1
dum

generate dum first none last none setup warn noangle nodihedral

join @residue dum renumber

!preparation of IC table for surface of interest

ic edit
dihe 3 dum  1 H13  1 C1   1 C2    0.0
dihe 2 oh2  1 H13  3 dum  1 C1  180.0
dihe 2 h1   2 oh2  1 H13  3 dum  90.0
dihe 2 h2   2 oh2  1 H13  3 dum -90.0

angl 3 dum  1 H13  1 C1   90.0
angl 2 oh2  1 H13  3 dum  90.0
angl 2 h1   2 oh2  1 H13  127.74
angl 2 h2   2 oh2  1 H13  127.74

bond 3 dum  1 H13  1.
bond 2 oh2  1 H13  @d !varied
bond 2 h1   2 oh2  0.9572
bond 2 h2   2 oh2  0.9572

end



ic print
ic build
if NoDrude .eq. false then
   coor sdrude
   coor shake
endif
coor print

update cutnb 999.0 ctofnb 998.0 ctonnb 997.0 switch vswitch inbfrq 100
    cons harm force 10000000.0 sele .not. type D* show end
    cons harm force 10000000.0 sele type dum show end
    MINI SD nstep 200 tolgrd 0.00001
    MINI ABNR nstep 500 tolgrd 0.00001
    cons harm force 0.0 sele all end
    coor print

set u 1000. ! minimum energy

!output file for water distance optimization
!open unit 99 write format name @residue_water_charmm.surf

label loop_1

energy
set cele ?elec
set cvdw ?vdw
set cener ?ener

calc eo = @cele - @mele - @wele
calc ex = @cvdw - @mvdw - @wvdw
calc em = @cener - @mener - @wener

!check for lowest energy and store data
set n @d
set o @eo
set x @ex
set m @em

if @u lt @m goto done_1
set u @m

incr d by 0.01
ic edit
bond 2 oh2  1 H13  @d !varied
end

!initialize water coordinates
coor init sele resn SWM4 .or. resn dum end

ic build
if NoDrude .eq. false then
   coor sdrude
   coor shake
endif

    cons harm force 10000000.0 sele .not. type D* show end
    cons harm force 10000000.0 sele type dum show end
    MINI SD nstep 200 tolgrd 0.00001
    MINI ABNR nstep 500 tolgrd 0.00001
    cons harm force 0.0 sele all end
    coor print

prnlev -5


if @d lt 4.5 goto loop_1
label done_1
decr d by 0.01

prnlev 5

delete atom sele type dum end

open unit 30 write form name wat/@residue_wat1.pdb 
write coor pdb unit 30 sele .not. ( type D* .or. type LP* .or. type OM) end
* @residue
*

!accumulate statistics   
calc count = @count + 1  
set ai  -1.7285360744   ! Single point interaction energies with BSSE correction 
set aidist 2.33188315   ! distance between the water and the atom
calc diff = @m - @ai 
calc sum = @sum + @diff 
calc diff2 = abs(@diff*@diff) 
calc sum2 = @sum2 + @diff2  
calc asum = @asum + abs(@diff)  

calc distdiff = @n - @aidist  

! write ab initio and empirical information to unit 50
echo 1) H13..OH2  90.0 degrees
echo a.i. @ai @aidist
echo emp. @m @o @x @n
echo ene diff: @diff dist diff: @distdiff

delet atom sele resn SWM4 .or. resn dum end


stop
