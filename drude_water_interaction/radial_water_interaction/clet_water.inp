* Water interactions
* 

ioformat extended

system "mkdir wat"

set residue CLET
set bombover 0 !needs to be set to -1 for 3-membered rings

set stage 5 
            

set NoDrude false


!### Things that usually shouldn't be changed. ####
set dontmini 0 ! Don't minimize main conformation. Should normally not be used.

!non-bond parameters
set 3 999.0  ! cutim
set 4 999.0  ! cutnb
set 5 980.0  ! ctonnb
set 6 990.0  ! ctofnb
set 7 switch
set 8 atom
set 9 vatom

!stream in files
    
    stream toppar_drude_master_example.str 
    stream toppar_drude_example.str

!SINCE inter is not correctly implemented for drude ff, the interaction should be computed:
!interaction = E_mol_wat - E_wat - E_mol
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!compute the energy of water firstly: in fact, the energy for swm4 is 0.00
read sequence card
* water
*
1
SWM4

generate SWM4 first none last none setup warn noangle nodihedral drude dmass 0.4

ic seed swm4 1 h1 swm4 1 oh2 swm4 1 h2
ic build
if NoDrude .eq. false then
   coor sdrude
   coor shake
endif
coor print

update cdie inbfrq -1 ihbfrq 0 -
@7 @8 @9 vswitch cutnb @4 ctofnb @6 ctonnb @5

cons harm force 1000.0 sele .not. type D* show end
cons harm force 1000.0 sele type dum show end
MINI SD nstep 200 tolgrd 0.00001
MINI ABNR nstep 500 tolgrd 0.00001
cons harm force 0.0 sele all end
coor print

energy

set wele ?elec
set wvdw ?vdw
set wener ?ener
write coor pdb name swm4.pdb

delete atom select all end
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
read sequence card
* @residue
*
1
@residue

bomlev @bombover
if NoDrude .eq. true then
   generate  @residue first none last none setup warn !drude dmass 0.4
else
   generate  @residue first none last none setup warn drude dmass 0.4
endif
bomlev 0

open read card unit 21 name @residue_mp2.crd
read coor card unit 21



if NoDrude .eq. false then
   coor sdrude
   coor shake
endif
coor copy comp
coor print ! use to make sure parent coords are not modified

update cdie inbfrq -1 ihbfrq 0 -
@7 @8 @9 vswitch cutnb @4 ctofnb @6 ctonnb @5

if @stage eq 4 then
  open unit 50 write form name @residue_water_constr.ene
  open unit 51 write form name @residue_water_constr.plt ! File with CHARMM computed energies 
else
  if @dontmini eq 0 then

    ! minimize Drude coordinates
    cons harm force 10000000.0 sele .not. type D* show end
    cons harm force 10000000.0 sele type dum show end
    MINI SD nstep 200 tolgrd 0.00001
    MINI ABNR nstep 500 tolgrd 0.00001
    cons harm force 0.0 sele all end
  endif
  if @stage ne 5 stop

  open unit 50 write form name @residue_water.ene
endif

!compute the energy for the molecule only
energy

set mele ?elec
set mvdw ?vdw
set mener ?ener

!water interactions: variables for averages etc.
set count 0
set asum 0.0
set sum 0.0
set sum2 0.0
coor print

coor dipole oxyz select segid @residue end
echu 50
echo @residue water interaction
if @stage eq 4 echo ######## USING MP2 GEOMETRY!!! ########
echo QM dipole (Debye)
echo mp2/cc-pvQZ:      X=  -1.1386    Y=     1.7948    Z=     0.0000  Tot=     2.1254 
echo mm dipole: X=  ?xdip      Y=     ?ydip     Z=     ?zdip   Tot=    ?rdip
echo

stream polar.efield.str
echo QM alph(scaled)
echo mp2/cc-pvQZ*1 XX= 6.249 YY= 6.359 ZZ= 4.962 Tot= 5.857
echo empirical       alpha: xx= @alphaxx yy= @alphayy  zz=  @alphazz  tot= @malpha
echo mp2/cc-pvQZ*0.85 XX= 5.312 YY= 5.405 ZZ= 4.217 Tot= 4.978
echo 

calc xperct = 6.249/@alphaxx
calc yperct = 6.359/@alphayy
calc zperct = 4.962/@alphazz
echo qm/mm-alpha     XX= @xperct YY= @yperct ZZ= @zperct 
echo 

ic delete sele all end
coor copy comp
!!! ***** BEGIN of water interactions ***** !!!
!12) Cl...OHH  Cl as donor
open unit 52 write form name @residue_water_do1.plt ! File with CHARMM computed energies 

set d 2.10 ! initial interaction distance
coor copy

read sequence card
* water
*
1
SWM4

generate SWM4 first none last none setup warn noangle nodihedral drude dmass 0.4

join @residue SWM4 renumber

read sequence card
* dummy
*
1
dum

generate dum first none last none setup warn noangle nodihedral

join @residue dum renumber

ic fill preserve
ic edit
dihe 3 dum  1 CL11  1 C1   1 C2   90.0
dihe 2 oh2  1 CL11  3 dum  1 C1  180.0
dihe 2 h1   2 oh2   1 CL11  3 dum   90.0
dihe 2 h2   2 oh2   1 CL11  3 dum  -90.0

bond 3 dum  1 CL11  1 !varied
bond 2 oh2  1 CL11  @d
bond 2 h1   2 oh2   0.9572
bond 2 h2   2 oh2   0.9572

angl 3 dum  1 CL11  1 C1     90.0
angl 2 oh2  1 CL11  3 dum    90.0
angl 2 h1   2 oh2   1 CL11 127.74
angl 2 h2   2 oh2   1 CL11 127.74
end

ic print
ic build
coor sdrude
coor shake

update cutnb 999.0 ctofnb 998.0 ctonnb 997.0 switch vswitch inbfrq 100
    cons harm force 10000000.0 sele .not. type D* show end
    cons harm force 10000000.0 sele type dum show end
    MINI SD nstep 200 tolgrd 0.00001
    MINI ABNR nstep 500 tolgrd 0.00001
    cons harm force 0.0 sele all end

set u 1000. ! minimum energy

label loop_12 

energy
set cele ?elec
set cvdw ?vdw
set cener ?ener

calc eo = @cele - @mele - @wele
calc ex = @cvdw - @mvdw - @wvdw
calc em = @cener - @mener - @wener

!check for lowest energy and store data
set n @d
set o @eo
set x @ex
set m @em

write title unit 52
*@d @m @o @x
*

if @d gt 5.1 goto done_12
set u @m

incr d by 0.1
ic edit
bond  2 oh2  1 CL11   @d
end

!initialize water coordinates
coor init sele resn SWM4 .or. resn dum end

ic build
coor sdrude
coor shake
    
    cons harm force 10000000.0 sele .not. type D* show end
    cons harm force 10000000.0 sele type dum show end
    MINI SD nstep 200 tolgrd 0.00001
    MINI ABNR nstep 500 tolgrd 0.00001
    cons harm force 0.0 sele all end

open unit 30 write form name wat/@residue_water_@d.pdb
write coor pdb unit 30 sele .not. ( type D* .or. type OM) end
* @residue
*


if @d lt 5.1 goto loop_12
label done_12
decr d by 0.1

prnlev 5


delet atom sele resn SWM4 .or. resn dum end
endif

stop
