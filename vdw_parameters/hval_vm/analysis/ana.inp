* MD simulation of a box of NMA
*

set resi nma
set num 128
set boxsize 26.0!23.3
set job run3
set seed 917847


!ioformat noextended

set cutim  16.0  ! cutim
set cutnb  16.0  ! cutnb
set ctonnb 10.0  ! ctonnb
set ctofnb 12.0  ! ctofnb
set eatom atom
set etrun switch
set vatom vatom
set vtrun vswitch


!--- Read parameters, psf and coordinates
stream toppar.str

read sequence @resi @num
generate @resi FIRST NONE LAST NONE SETUP WARN DRUDE DMASS 0.4

open unit 10 read form name nma_liq@num_fix.crd
read coor card unit 10

!ic para
!ic build
coor sdrude
coor shake
!coor print

ener

! Setup crystal ----------------------------------------------------------------

crystal define cubic @boxsize  @boxsize  @boxsize  90.0 90.0 90.0
crystal build noper 0 cutoff @cutim

image byres xcen 0.0 ycen 0.0 zcen 0.0 select resname @resi end

update inbfrq -1 imgfrq -1 ihbfrq 0 -
ewald pmewald kappa 0.34 fftx 32 ffty 32 fftz 32 order 6 lrc -
@eatom @etrun @vatom @vtrun cutnb @cutnb ctonnb @ctonnb ctofnb @ctofnb -
cutim @cutim

! SHAKE!!!
coor copy comp
SHAKE bonh param tolerance 1.0e-9 -
      nofast -
      select ( .not. (type D*)) end -
      select ( .not. (type D*)) end noreset

cons harm force 10000000.0 sele .not. type D* end
mini SD nstep 200 tolgrd 0.00001
cons harm force 0.0 sele .not. type D* end

! start analysis
set i 1 ! ignore the first 1 ns
set f 10

open write unit 30 card name xtal.@job.@temp.dat
open write unit 31 card name energy.@job.@temp.dat
open write unit 32 card name dipole.@job.@temp.dat

label m_traj

 open read unit 20 file name nma.box@num.@temp.@job.@i.dcd
 traj query unit 20

 set j 0
 label readFrame
        calc frame = ?START + ?SKIP  * @j
        coor dyna first 20 nunit 1 skip ?SKIP begin @frame stop @frame

write title unit 30
*?XTLA
*

energy

write title unit 31
*?ener
*

coor dipole sele all end

write title unit 32
*?rdip ?xdip ?ydip ?zdip
*

      incr j by 1
      if j lt ?NFILE goto readFrame
incr i by 1
if i .le. @f goto m_traj


stop


